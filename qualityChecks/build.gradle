buildscript {
    ext.kotlin_version = '1.1.2-2'
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.gradle.java-gradle-plugin'
apply plugin: 'com.gradle.plugin-publish'

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile 'junit:junit:4.12'
}

sourceSets {
    functionalTest {
        kotlin {
            srcDir file('src/functionalTest/kotlin')
        }
        resources {
            srcDir file('src/functionalTest/resources')
        }
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task functionalTest(type: Test) {
    group = 'verification'
    testClassesDir = sourceSets.functionalTest.output.classesDir
    classpath = sourceSets.functionalTest.runtimeClasspath
}

tasks.withType(Test) {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

check.dependsOn functionalTest

version = "0.2.3"
group = "info.adavis"

gradlePlugin {
    testSourceSets sourceSets.functionalTest

    plugins {
        qualityChecksPlugin {
            id = "info.adavis.qualitychecks"
            implementationClass = "info.adavis.qualitychecks.QualityChecksPlugin"
        }
    }
}

pluginBundle {
    website = 'https://github.com/adavis/quality-checks'
    vcsUrl = 'https://github.com/adavis/quality-checks.git'
    description = 'Simple Gradle Plugin for including Checkstyle, FindBugs, and PMD in your project.'
    tags = ['Checkstyle', 'FindBugs', 'PMD']

    //noinspection GroovyAssignabilityCheck
    plugins {
        qualityChecksPlugin {
            id = 'info.adavis.qualitychecks'
            displayName = 'Quality Checks Plugin'
        }
    }
}